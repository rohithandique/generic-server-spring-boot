plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id "org.sonarqube" version "7.2.1.6560"
	id 'com.github.ben-manes.versions' version '0.53.0'
	id 'com.diffplug.spotless' version '8.1.0'
	id "com.github.davidmc24.gradle.plugin.avro" version "1.9.1"
}

group = 'com.generic'
version = '0.0.1-SNAPSHOT'

sourceSets {
	main {
		java {
			srcDirs += 'build/generated-main-avro-java'
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sonar {
	properties {
		property "sonar.projectKey", "rohithandique_generic-server-spring-boot"
		property "sonar.organization", "rohithandique"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml').get().asFile.absolutePath
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://packages.confluent.io/maven/"
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.hawt:hawtio-springboot:4.1.0'
	implementation 'net.logstash.logback:logstash-logback-encoder:9.0'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.apache.avro:avro:1.12.1'
	implementation 'io.confluent:kafka-avro-serializer:8.1.1'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

avro {
	createSetters = true
	fieldVisibility = "PRIVATE"
	outputCharacterEncoding = "UTF-8"
}

spotless {
	java {
		target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
		googleJavaFormat()
		removeUnusedImports()
	}
	format 'misc', {
		target '**/*.md', '**/.gitignore', '**/*.properties'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootRun') {
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs = [
		'-Dcom.sun.management.jmxremote',
		'-Dcom.sun.management.jmxremote.port=9010',
		'-Dcom.sun.management.jmxremote.ssl=false',
		'-Dcom.sun.management.jmxremote.authenticate=false',
		'-Dcom.sun.management.jmxremote.rmi.port=9010',
		'-Dspring.profiles.active=local'
	]
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

tasks.named('sonar').configure {
	dependsOn tasks.named('test'), tasks.named('jacocoTestReport')
}

bootJar {
	archiveFileName = "${rootProject.name}.jar"
}