plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id "org.sonarqube" version "7.2.1.6560"
	id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'com.generic'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Configuration block for the SonarQube plugin
sonar {
	properties {
		// Essential properties - replace with your actual keys
		property "sonar.projectKey", "rohithandique_generic-server-spring-boot"
		property "sonar.organization", "rohithandique"
		property "sonar.host.url", "https://sonarcloud.io"

		// Properties for coverage reporting (required for New Code metrics)
		property "sonar.java.coveragePlugin", "jacoco"
		// This path tells SonarScanner where to find the coverage report
		property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml').get().asFile.absolutePath
	}
}

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	developmentOnly
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.hawt:hawtio-springboot:4.1.0'
	implementation 'net.logstash.logback:logstash-logback-encoder:9.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootRun') {
	classpath = sourceSets.main.runtimeClasspath + configurations.developmentOnly
	jvmArgs = [
		'-Dcom.sun.management.jmxremote',
		'-Dcom.sun.management.jmxremote.port=9010',
		'-Dcom.sun.management.jmxremote.ssl=false',
		'-Dcom.sun.management.jmxremote.authenticate=false',
		'-Dcom.sun.management.jmxremote.rmi.port=9010',
		'-Dspring.profiles.active=local'
	]
}

// Task to ensure JaCoCo report is generated as XML
jacocoTestReport {
	reports {
		xml.required = true
	}
}

// Ensure JaCoCo report runs before the SonarQube analysis task
tasks.named('sonar').configure {
	dependsOn tasks.named('test'), tasks.named('jacocoTestReport')
}

bootJar {
	archiveFileName = "${rootProject.name}.jar"
}